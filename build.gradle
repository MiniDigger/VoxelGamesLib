group = "me.minidigger.voxelgameslib"

defaultTasks "build"

buildscript {
    repositories {
        maven {
            url "https://jcenter.bintray.com/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath group: 'org.zeroturnaround', name: 'gradle-jrebel-plugin', version: '1.1.3'
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'rebel'

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    version = "0.0.1-SNAPSHOT"

    repositories {
        mavenCentral()
        maven {
            url "http://ci.emc.gs/nexus/content/repositories/aikar/"
        }
        maven {
            url "https://jcenter.bintray.com/"
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencies {
        testCompile "junit:junit:4.11"
        compileOnly "org.projectlombok:lombok:1.14.8"
    }

    shadowJar {
        dependencies {
            exclude(dependency("junit:junit"))
        }
    }

    rebel {
        alwaysGenerate = true
        showGenerated = false
        rootPath = getRootProject().getProjectDir().getAbsolutePath()
        classpath {
            resource {
                directory = "survivalgames/build/classes/main"
            }
            resource {
                directory = "bukkit/build/classes/main"
            }
            resource {
                directory = "api/build/classes/main"
            }
            resource {
                directory = "bungeecord-chat/build/classes/main"
            }
        }
    }

    test {
        reports.html.enabled = true
        filter {
            includeTestsMatching "*Test*"
        }
    }

    build.dependsOn(generateRebel)
}

task testReport(type: TestReport) {
    println "test report exported to $buildDir/reports/allTests"
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects*.test
}

println """
*********************************************************
You are compiling VoxelGamesLib
if you encounter any issues, create a issue on github
https://github.com/MiniDigger/VoxelGamesLib/issues
*********************************************************
"""
