dependencies {
    compileOnly "org.bukkit:craftbukkit:1.11-R0.1-SNAPSHOT"
    compile "co.aikar:taskchain-bukkit:3.4.3"

    compile project(path: ":api", configuration: 'shadow')

    compile project(":survivalgames")
}

repositories {
    mavenLocal()
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
}

processResources {
    filesMatching('**/plugin.yml') {
        filter {
            it.replace('@version@', project.getVersion())
        }
    }
}

task copyPluginToTestServer(type: Copy) {
    from("${rootProject.projectDir}\\bukkit\\build\\libs\\") {
        include "**/*-all.jar"
    }
    into "${rootProject.projectDir}\\testserver\\plugins\\"
}

task deleteOldLogFiles(type: Delete) {
    def cutoff = new Date().minus(1) //24 hrs ago

    delete fileTree(dir: 'testserver/logs/')
            .matching { include '*.log' }
            .findAll {
        def filedate = new Date(it.lastModified())
        filedate.before(cutoff)
    }
}

build.finalizedBy(shadowJar)
shadowJar.finalizedBy(copyPluginToTestServer)
shadowJar.finalizedBy(deleteOldLogFiles)